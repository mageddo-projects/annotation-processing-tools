buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
  }
}
plugins {
  id "io.codearte.nexus-staging" version "0.21.1" // close and release sonatype artifcats
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
  throw new UnsupportedOperationException(
      "Please use JDK 8 to compile this project, current: ${JavaVersion.current()}"
  )
}

subprojects {

  apply plugin: "java"

  repositories {
    mavenLocal()
    mavenCentral()
  }

  sourceCompatibility = JavaVersion.VERSION_1_8

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "failed"
      exceptionFormat "full"
    }
  }
}

nexusStaging {
  packageGroup = 'com.mageddo'
  username = project.findProperty("ossrhUsername")
  password = project.findProperty("ossrhPassword")
}

configure([
    project(":aptools"), project(":lombok-ext"), project(":reflection-config-generator")
]){
  apply from: "${rootProject.rootDir}/nexus.gradle"
}
